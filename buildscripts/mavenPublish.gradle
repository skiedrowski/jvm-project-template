// Required properties:
// MVN_PUBLISH_URL_PREFIX     - we'll append "/snapshots" or "/releases" depending on the project.version
// MVN_USER
// MVN_PASSWORD
//
// may be set via gradle.properties (systemProp.MVN_USER=fred) or via commandline (-DMVN_USER=fred)
//
// The script also depends on valid
//      version = <maven version>
//      group = <maven group id>
//
// This script requires a sourcesJar artifact (i.e. as built by sourceJar.gradle) and a testJar artifact (i.e. as built by testJar.gradle)
//
// currently, only one maven repo is supported; to use different servers (i.e. for snapshots/release), we'd need to
// duplicate the properties and further complicate the usage. Let us wait for the use case ;-).
//

apply plugin: 'maven-publish'

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java

			artifact jarTestClasses {
				classifier 'test'
			}
			artifact sourcesJar {
				classifier 'sources'
			}
		}
	}

	repositories {
		maven {
			if(project.version.endsWith('-SNAPSHOT')) {
				url "${System.properties['MVN_PUBLISH_URL_PREFIX']}/snapshots"
			} else {
				url "${System.properties['MVN_PUBLISH_URL_PREFIX']}/releases"
			}
			credentials {
				username System.properties['MVN_USER']
				password System.properties['MVN_PASSWORD']
			}
		}
	}
}
