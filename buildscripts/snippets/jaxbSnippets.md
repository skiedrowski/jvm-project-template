This build script is pretty similar to [jaxwsSnippets](jaxwsSnippets.md).

    configurations { jaxb }

    dependencies {
        jaxb "com.sun.xml.bind:jaxb-xjc:$ver.jaxb"
        jaxb "com.sun.xml.bind:jaxb-impl:$ver.jaxb"
        //jaxb2 plugin used for toString, inheritance and other features
        jaxb "org.jvnet.jaxb2_commons:jaxb2-basics:$ver.jaxb2_basics"
        jaxb "org.jvnet.jaxb2_commons:jaxb2-basics-runtime:$ver.jaxb2_basics"
        jaxb "org.jvnet.jaxb2_commons:jaxb2-basics-tools:$ver.jaxb2_basics"
        jaxb "org.jvnet.jaxb2_commons:jaxb2-basics-ant:$ver.jaxb2_basics"
    }

    sourceSets {
        jaxbHelper {
            //contains all sources which are referenced by jaxb-XSDs/XJBs
            compileClasspath = main.compileClasspath
        }

        jaxb {
            //sources generated by xjc
            compileClasspath = main.compileClasspath + jaxbHelper.output
            //runtimeClasspath += main.output
        }

        main {
            compileClasspath += jaxb.output + jaxbHelper.output
            runtimeClasspath += jaxb.output + jaxbHelper.output //+ jaxb.runtimeClasspath
        }

        test {
            compileClasspath += jaxb.output + jaxbHelper.output
            runtimeClasspath += jaxb.output + jaxbHelper.output
        }
    }

    jar {
        from sourceSets.jaxb.output
        from sourceSets.jaxbHelper.output
    }

    task jarJaxbClasses(type: Jar, description: "Assembles a jar archive containing jaxb classes of project $project.name", group: 'build') {
        appendix = 'jaxb'
        dependsOn classes
        from sourceSets.jaxb.output
        from sourceSets.jaxbHelper.output
    }

    artifacts {
        jaxb jarJaxbClasses
    }

    task jaxb() {
        doLast {
            System.setProperty('javax.xml.accessExternalSchema', 'all')
            ant.taskdef(name: 'xjc', classname: 'org.jvnet.jaxb2_commons.xjc.XJC2Task', classpath: configurations.jaxb.asPath)
            ant.jaxbTargetDir = sourceSets.jaxb.java.srcDirs.last()
            ant.jaxbTargetDir.mkdirs()
            ant.xjc(destdir: "${ant.jaxbTargetDir}", /*encoding: 'UTF-8',*/ extension: 'true', target: '2.1' ) {
                schema(dir: 'build/resources/main', includes: "**/*.xsd")
                binding(dir: 'build/resources/main', includes: "**/*.xjb")
                arg(value: '-npa')
                arg(value: '-Xinheritance')
                arg(value: '-XtoString') //requires jabx2-basics
            }
        }
    }
    compileJava.dependsOn jaxb
    jaxb.dependsOn processResources	